
header "sys" as sys;
header "openssl" as ossl;

use mem;
use os;

class SocketTCP async {
	~ host: String;
	~ fd: i32;
	~ port: u16;
	~ blocking: bool = true;
	- addrinfo: sys.cstruct_addrinfo;
	- connection: ?Connection = null;

	static fn new(host: String, port: u16, blocking: bool = true) SocketTCP !os_socket_create !invalid_host {

        #if OS == win
		os:WSA_init();
		#end

        #if OS == linux
		let flags = sys.OS.SOCK_STREAM;
		if !blocking: flags = flags | sys.OS.SOCK_NONBLOCK;
    	let fd = sys.socket(sys.OS.AF_INET, flags, 0);
		#else
		// Win & macos
    	let fd = sys.socket(sys.OS.AF_INET, sys.OS.SOCK_STREAM, 0);
        #if OS == macos
		let flags = sys.fcntl(fd, sys.OS.F_GETFL, 0);
		if flags >= 0 && !blocking : sys.fcntl(fd, sys.OS.F_SETFL, flags | sys.OS.O_NONBLOCK);
		#end
        #if OS == win
		rep fd = os:WSA_unix_fd(fd);
		#end
		#end

    	if fd == -1 {
        	//#if OS == win
			//println("WSA socket error: " + sys.WSAGetLastError().str());
			//#end
			throw os_socket_create;
    	}

		let addrinfo = CLASS.get_addrinfo(host, port) !! throw invalid_host;;

		return SocketTCP{
			host: host,
			port: port,
			fd: fd,
			blocking: blocking,
			addrinfo: addrinfo,
		};
    }

	fn connect() Connection !connection_failed {

		let c = this.connection ?? {{
			let c = Connection {
				fd: this.fd
				open: false,
			};
			let res = &c;
			this.connection = c;
			return res;
		}};

		if !c.open {
        	#if OS == win
			let err = sys.connect(this.fd, this.addrinfo.ai_addr, this.addrinfo.ai_addrlen @as i32);
			#else
			let err = sys.connect(this.fd, this.addrinfo.ai_addr, this.addrinfo.ai_addrlen);
			#end
			if err != 0 : throw connection_failed;
			c.open = true;
		}

		return c;
	}

	fn bind() !get_addr_info !bind !listen {
		let yes : i32 = 1;
		let yes_ptr = @ptr_of(yes);
    	sys.setsockopt(this.fd, sys.OS.SOL_SOCKET, sys.OS.SO_REUSEADDR, yes_ptr, @sizeof(i32));
		
		let err : i32 = 0;
        #if OS == win
    	err = sys.bind(this.fd, this.addrinfo.ai_addr, this.addrinfo.ai_addrlen @as i32);
		#else
    	err = sys.bind(this.fd, this.addrinfo.ai_addr, this.addrinfo.ai_addrlen);
		#end
		if err != 0 {
			throw bind;
		}

    	err = sys.listen(this.fd, 2000000);
		if err != 0 {
			throw listen;
		}

	}

	- static fn get_addrinfo(host: *String, port: u16) sys.cstruct_addrinfo !failed {

		let addrinfo : ?sys.cstruct_addrinfo = null;
		let hints = @stack_object(sys.cstruct_addrinfo);
		mem:clear(hints @as ptr, @sizeof_class(sys.cstruct_addrinfo));
		hints.ai_family = sys.OS.AF_INET;
		hints.ai_socktype = sys.OS.SOCK_STREAM;
		hints.ai_flags = sys.OS.AI_PASSIVE;

		let chost = host.to_c_string();
		let cport = port.str().to_c_string();
    	let err = sys.getaddrinfo(chost, cport, hints, @array_of(addrinfo));
		if err != 0 {
			throw failed;
		}
		rep addrinfo = addrinfo ?! {
			throw failed;
		};
		chost.free();
		cport.free();

		return addrinfo;
	}

	fn accept() Connection !error {
        #if OS == linux
		let fd = sys.accept4(this.fd, null, null, sys.OS.SOCK_NONBLOCK);
		#else
		let fd = sys.accept(this.fd, null, null);
        #if OS == macos
		let flags = sys.fcntl(fd, sys.OS.F_GETFL, 0);
		if flags >= 0 : sys.fcntl(fd, sys.OS.F_SETFL, flags | sys.OS.O_NONBLOCK);
		#end
        #if OS == win
		rep fd = os:WSA_unix_fd(fd);
		#end
		#end
		if fd == -1 {
			throw error;
		}
		return Connection {
			fd: fd,
		};
	}

	fn clone() CLASS {
		return CLASS {
			host: this.host + "",
			fd: this.fd,
			port: this.port,
			blocking: this.blocking,
			addrinfo: CLASS.get_addrinfo(this.host, this.port) !? this.addrinfo @as sys.cstruct_addrinfo,
		};
	}

	fn __before_free() {
		sys.freeaddrinfo(this.addrinfo);
	}
}