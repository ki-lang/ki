
#if OS == win
header "sys" as sys;
#else
header "pthread" as pt;
#end

use ki:mem;

struct ThreadEntry[F, T] {
	data: T;
	start_func: F;
}

class Thread[T] {

	#if OS == win
	- os_handle: sys.HANDLE;
	#else
	- os_thread: pt.pthread_t;
	#end

	#if TYPE_IS_VOID T

	- static fn entry(data: ptr) void {
		let func = data @as fn()(void);
		func();
	}

	static fn start(start_func: fn()(void)) CLASS !fail {

		#if OS == win
		let thr = sys.CreateThread(null, 0, CLASS.entry @as ptr, start_func @as ptr, 0, null);
		rep thr = thr ?! throw fail;;
		let t = CLASS{ os_handle: thr };
		#else
    	let thr = mem:alloc(sizeof_class(pt.pthread_t)) @as pt.pthread_t;
    	let err = pt.pthread_create(thr, null, CLASS.entry, start_func @as ptr);
    	if err != 0 {
			throw fail;
    	}
		let t = CLASS{ os_thread: thr };
		#end

    	return t;
	}

	#else

	- static fn entry(data: ptr) void {
		let entry = data @as ThreadEntry[fn(T)(void), T];
		entry.start_func(entry.data @as T);
		mem:free(entry @as ptr);
	}

	static fn start(start_func: fn(T)(void), data: async T) CLASS !fail {

		let entry_data = ThreadEntry[fn(T)(void), T]{
			start_func: start_func,
			data: data
		};

		#if OS == win
		let thr = sys.CreateThread(null, 0, CLASS.entry @as ptr, entry_data @as ptr, 0, null);
		rep thr = thr ?! throw fail;;
		let t = CLASS{ os_handle: thr };
		#else
    	let thr = mem:alloc(sizeof_class(pt.pthread_t)) @as pt.pthread_t;
    	let err = pt.pthread_create(thr, null, CLASS.entry, entry_data @as ptr);
    	if err != 0 {
			throw fail;
    	}
		let t = CLASS{ os_thread: thr };
		#end

    	return t;
	}

	#end

	fn terminate() {
		#if OS == win
		let check = sys.TerminateThread(this.os_handle, 0);
		#else
		// TODO
		#end
	}
}

