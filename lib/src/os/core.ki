
header "sys" as sys;

fn system(cmd: *String) i32 {
	let ccmd = cmd.to_c_string();
	let code = sys.system(ccmd);
	ccmd.free();
	return code;
}
fn exec(cmd: *String) String !failed {

	let buf = ByteBuffer.new(1024);

	let ccmd = cmd.to_c_string();
	let cstr_r = "r".to_c_string();
    let pipe = sys.popen(ccmd, cstr_r);
	ccmd.free();
	cstr_r.free();

	rep pipe = pipe ?! throw failed;;

	while true {
		buf.minimum_free_space(1024);
		let part = sys.fgets(buf.data + buf.length, 1024, pipe);
		rep part = part ?! break;;
		buf.append_str(part.to_str());
	}

    sys.pclose(pipe);
    return buf.to_str();
}

fn sleep_ms(ms: uxx) void {
	#if OS == win
	sys.Sleep(ms @as u32);
	#else
	let msec = ms @as ixx;
	let ts = @stack_object(sys.cstruct_timespec);
    ts.tv_sec = msec / 1000;
    ts.tv_nsec = (msec % 1000) * 1000000;
    sys.nanosleep(ts, ts);
	#end
}

fn sleep_ns(ns: uxx) void {
	#if OS == win
	sys.Sleep(1);
	#else
	let nsec = ns @as ixx;
	let ts = @stack_object(sys.cstruct_timespec);
    ts.tv_sec = nsec / 1000000000;
    ts.tv_nsec = (nsec % 1000000000);
    sys.nanosleep(ts, ts);
	#end
}

fn !panic(msg: *String) void {
	print("PANIC ERROR : ");
	print(msg);
	print("\n");
	exit(1);
}

fn !exit(code: i32) void {
	sys.exit(code);
}

